plugins {
	id "com.github.ethankhall.semantic-versioning" version "1.1.0"
	id "io.spring.dependency-management" version "0.5.4.RELEASE"
}

apply plugin: 'base'

def ossrhReleaseUrl = hasProperty('ossrhReleaseUrl') ? ossrhReleaseUrl : System.getenv('ossrhReleaseUrl')
def ossrhSnapshotUrl = hasProperty('ossrhSnapshotUrl') ? ossrhSnapshotUrl : System.getenv('ossrhSnapshotUrl')
def ossrhUsername = hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('ossrhUsername')
def ossrhPassword = hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('ossrhPassword')

subprojects {
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.github.ethankhall.semantic-versioning'

	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'

	version.with { major = 0; minor = 1; patch = 0 }
	group = 'com.github.stocky37'

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom 'io.dropwizard:dropwizard-bom:0.9.+'
			mavenBom 'com.yammer.tenacity:tenacity-bom:0.8.+'
		}

		dependencies {
			dependency 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.6.+'
			dependency 'io.dropwizard.modules:dropwizard-java8:0.9.+'
			dependency 'com.smoketurner:dropwizard-swagger:0.9.+'
			dependency 'org.springframework:spring-core:4.+'
			dependency 'redis.clients:jedis:2.8.+'
			dependency 'nl.jqno.equalsverifier:equalsverifier:2.+'
			dependency 'junit:junit:4.12'
			dependency 'uk.co.datumedge:hamcrest-json:0.+'
			dependency 'org.hamcrest:hamcrest-all:1.+'
			dependency 'commons-io:commons-io:2.+'
		}
	}

	dependencies {
		testCompile 'junit:junit'
		testCompile 'nl.jqno.equalsverifier:equalsverifier'
	}

	compileJava {
		options.compilerArgs << '-parameters'
	}

	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	task sourceJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifactId = "$rootProject.name-$project.name"

				from components.java

				artifact javadocJar
				artifact sourceJar

				pom.withXml {
					asNode().children().last() + {
						resolveStrategy = Closure.DELEGATE_FIRST
						name "$project.ext.name"
						description "$project.ext.description"
						url 'https://github.com/stocky37/dropwizard-util'

						scm {
							url 'git@github.com:stocky37/dropwizard-util.git'
							connection 'git@github.com:stocky37/dropwizard-util.git'
							developerConnection 'git@github.com:stocky37/dropwizard-util.git'
						}

						licenses {
							license {
								name 'MIT License'
								url 'http://www.opensource.org/licenses/mit-license.php'
							}
						}

						developers {
							developer {
								id 'stocky37'
								name 'Tom Stockwell'
								email 'tom@stockwells.me'
							}
						}
					}
				}
			}
		}

		repositories {
//			maven {
//				url "file:/${project.buildDir}/artifacts"
//			}

			maven {
				if (project.version.releaseBuild) {
					url ossrhReleaseUrl
				} else {
					url ossrhSnapshotUrl
				}
				credentials {
					username = ossrhUsername
					password = ossrhPassword
				}
			}
		}
	}
}
